;GA_DEMO	Tremelo

;Pot0 = Reverb level

;Pot1 = Tremelo rate

;pot2 = Effect level



;memory declarations:



mem	ap1	334

mem	ap2	556

mem	ap3	871



mem	lap1a	808

mem	lap1b	1934

mem	d1	2489



mem	lap2a	1016

mem	lap2b	1787

mem	d2	2287



;register equates:



equ	mono	reg0

equ	apout	reg1

equ	lp1	reg2

equ	lp2	reg3

equ	revout	reg4

equ	trem	reg5



;declare constants:



equ	kap	0.6	;all pass coefficient

equ	krt	0.55	;reverb time

equ	krf	0.5	;reverb lpf freq

equ	krs	-0.6	;reverb lpf shelf



;clear registers and initialize LFOs:



skp	run,endclr

wrax	lp1,0

wrax	lp2,0

wlds	sin0,12,100

wlds	sin1,0,16383

endclr:



;sum inputs to mono:



rdax	adcl,0.5

rdax	adcr,0.5

wrax	mono,0.5	;attenuate into reverb



;do reverb and put result in revout (after pot0 control):



rda	ap1#,kap

wrap	ap1,-kap

rda	ap2#,kap

wrap	ap2,-kap

rda	ap3#,kap

wrap	ap3,-kap

wrax	apout,0



rda	d2#,krt

rdax	apout,1

rda	lap1a#,kap

wrap	lap1a,-kap

rda	lap1b#,kap

wrap	lap1b,-kap

rdfx	lp1,krf

wrlx	lp1,krs

wra	d1,0



rda	d1#,krt

rdax	apout,1

rda	lap2a#,kap

wrap	lap2a,-kap

rda	lap2b#,kap

wrap	lap2b,-kap

rdfx	lp2,krf

wrlx	lp2,krs

wra	d2,1.99

rda	d1,1.99

mulx	pot0

mulx	pot0

wrax	revout,0



;smooth reverb:



cho	rda,sin0,sin|reg|compc,lap1b+100

cho	rda,sin0,sin,lap1b+101

wra	lap1b+200,0

cho	rda,sin0,sin|reg|compc,lap2b+100

cho	rda,sin0,sin,lap2b+101

wra	lap2b+200,0



;Do tremelo, start with controlling LFO Sin1 with pot1:



rdax	pot1,1		;get control pot

mulx	pot1		;square function

sof	0.4,0.1		;scale rate to reasonable range

wrax	sin1_rate,0	;write to sin1 rate register



cho	rdal,sin1		;get sinewave, +/-0.5

sof	1,0.5		;add 1/2 to get sinwave that swings 0 to 1

wrax	trem,0		;write tremelo modulation signal



;now produce output:



rdax	mono,0.5	;attenuate signals so as not to clip

mulx	trem

sof	0.75,0		;boost tremelo to maintain average amplitude

rdax	mono,-0.5

mulx	pot2		;crossover between input and tremelo with pot2

rdax	mono,0.5

sof	1.999,0		;recover gain

rdax	revout,1

sof	1,0.2

wrax	dacl,1

sof	1,-0.4

wrax	dacr,0




