;GA_DEMO	Chorus

;Pot0 = Reverb level
;Pot1 = Rate
;pot2 = Effect level

;memory declarations:

mem	ap1	334
mem	ap2	556
mem	ap3	871

mem	lap1a	808
mem	lap1b	1934
mem	d1	2489

mem	lap2a	1016
mem	lap2b	1787
mem	d2	2287

mem	chodel	5000

;register equates:

equ	mono	reg0
equ	apout	reg1
equ	lp1	reg2
equ	lp2	reg3
equ	revout	reg4
equ	choout	reg5

;declare constants:

equ	kap	0.6	;all pass coefficient
equ	krt	0.55	;reverb time
equ	krf	0.5	;reverb lpf freq
equ	krs	-0.6	;reverb lpf shelf

;clear registers and initialize LFOs:

skp	run,endclr
wrax	lp1,0
wrax	lp2,0
wlds	sin0,12,100
wlds	sin1,0,800
endclr:

;sum inputs to mono:

rdax	adcl,0.5
rdax	adcr,0.5
wra	chodel,1		;write to chorus delay
wrax	mono,0.5	;attenuate into reverb

;do reverb and put result in revout (after pot0 control):

rda	ap1#,kap
wrap	ap1,-kap
rda	ap2#,kap
wrap	ap2,-kap
rda	ap3#,kap
wrap	ap3,-kap
wrax	apout,0

rda	d2#,krt
rdax	apout,1
rda	lap1a#,kap
wrap	lap1a,-kap
rda	lap1b#,kap
wrap	lap1b,-kap
rdfx	lp1,krf
wrlx	lp1,krs
wra	d1,0

rda	d1#,krt
rdax	apout,1
rda	lap2a#,kap
wrap	lap2a,-kap
rda	lap2b#,kap
wrap	lap2b,-kap
rdfx	lp2,krf
wrlx	lp2,krs
wra	d2,1.99
rda	d1,1.99
mulx	pot0
mulx	pot0
wrax	revout,0

;smooth reverb:

cho	rda,sin0,sin|reg|compc,lap1b+100
cho	rda,sin0,sin,lap1b+101
wra	lap1b+200,0
cho	rda,sin0,sin|reg|compc,lap2b+100
cho	rda,sin0,sin,lap2b+101
wra	lap2b+200,0
;

;now do chorus, prepare pot1 for rate, driving SIN1:

rdax	pot1,1
mulx	pot1
sof	0.02,0.01	;approx. 1Hz to 3 Hz
wrax	sin1_rate,0	;load sin1 rate register

;do chorus from chorus delay:

cho	rda,sin1,sin|reg|compc,chodel+1400
cho	rda,sin1,sin,chodel+1401

cho	rda,sin1,cos|reg|compc,chodel+1200
cho	rda,sin1,cos,chodel+1201

cho	rda,sin1,sin|reg|compa,chodel+1600
cho	rda,sin1,sin|compc|compa,chodel+1601

cho	rda,sin1,cos|reg|compa,chodel+900
cho	rda,sin1,cos|compc|compa,chodel+901

mulx	pot2
wrax	choout,0

;combine signals:

rdax	mono,1
rdax	revout,1
rdax	choout,0.5
sof	1,0.02
wrax	dacl,1
sof	1,-0.04
wrax	dacr,0



